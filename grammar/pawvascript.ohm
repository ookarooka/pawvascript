PawvaScript {
    Program         = Block
    Block           = (Statement)*
    Conditional     = if Exp then ":" Block (else if Exp then ":" Block)+ (else ":")? Block tail --elseif
                    | if Exp then ":" Block else ":" Block tail                                  --else
                    | if Exp then ":" Block tail                                                 --basic
    Loop            = chase Exp times ":" Block tail                                             --defined
                    | chase while Exp ":" Block tail                                             --while
                    | chase id through id ":" Block tail                                         --through
                    | chase VarDec by Exp while Exp ":" Block tail                               --for
                    | chase ":" Block tail                                                       --infinite
    Statement       = Declaration
                    | Assignment                    
                    | Print          
                    | Break                    
                    | Continue                      
                    | Loop                    
                    | Conditional                
                    | Return                         
                    | FuncCall ";"                                                               --funccall
    Assignment      = id Grouping? is Exp ";"
    Print           = (woof | bark | howl) Exp ";"
    Break           = poop ";"
    Continue        = walkies ";"
    Return          = give Exp? ";"
    Declaration     = FuncDec
                    | TypeDec
                    | VarDec ";"                                                                 --varDec
    FuncDec         = trick id (chews Parameters)? (fetches type Grouping?)? ":" Block tail                --basic
                    | trick id (chews Parameters)? (fetches type Grouping?) ";"                            --constructor
    VarDec          = type id Grouping? (is Exp)?
    TypeDec         = breed id is ":" Block tail
    Grouping        = "[" (type ":")? type "]"
    FuncCall        = QualifiedId "(" (Exp ("," Exp)*)? ")" 
    Parameters      = "[" (type Grouping? ":" id)? ("," type Grouping? ":" id)* "]"
    Exp             = FuncCall
                    | RelopExp
    RelopExp        = Term relop Term                                                            --relop
                    | Term
    Term            = Term (addOp | (~without with) | without) Factor                            --addOp
                    | Factor
    Factor          = Factor ("*" | "/" | mod) Negation                                          --mulOp
                    | Negation
    Negation        = prefixOp Factorial                                                         --preFix
                    | Factorial
    Factorial       = Primary postfixOp                                                          --postFix
                    | Primary                                                   
    Primary         = "(" Exp ")"                                                                --parens
                    | QualifiedId
                    | numlit
                    | strlit
                    | booleanlit
                    | nullval
                    | "[" (peanutButter? Exp)? ("," peanutButter? Exp)* "]"                      --pack
                    | "[" (Exp":"Exp)? ("," Exp":"Exp)* "]"                                      --kennel
    QualifiedId     = QualifiedId "\'s" id                                                       --property
                    | id 
    addOp           = "+" | "-"
    prefixOp        = "-" | not
    postfixOp       = "!"
    type            = toeBeans | leash | goodBoy | pack | kennel | breed | id
    nullval         = cat
    booleanlit      = good | bad
    numlit          = digit+ ("." digit+)?
    strlit          = "\"" (char | "'")* "\""
    char            = escape
                    | interpolation
                    | ~"\\" ~"\"" ~"'" ~"\n" ~"!" any
    interpolation   = "![" id "]"
    escape          = "\\\\" | "\\\"" | "\\'" | "\\n" | "\\!"
    relop           = "isGreaterThan" | "isAtLeast" | "isAtMost" | "isLessThan" | equalityop
    equalityop      = "equals" | "notEquals"
    is              = "is" ~alnum
    toeBeans        = "toeBeans" ~alnum
    leash           = "leash" ~alnum
    goodBoy         = "goodBoy" ~alnum
    pack            = "pack" ~alnum
    kennel          = "kennel" ~alnum
    breed           = "breed" ~alnum
    chase           = "chase" ~alnum
    tail            = "tail" ~alnum
    else            = "else" ~alnum
    bark            = "bark" ~alnum
    woof            = "woof" ~alnum
    howl            = "howl" ~alnum
    trick           = "trick" ~alnum
    if              = "if" ~alnum
    then            = "then" ~alnum
    good            = "good" ~alnum
    bad             = "bad" ~alnum 
    chews           = "chews" ~alnum
    fetches         = "fetches" ~alnum
    give            = "give" ~alnum
    with            = "with" ~alnum
    without         = "without" ~alnum
    not             = "not" ~alnum
    peanutButter    = "peanutButter" ~alnum
    times           = "times" ~alnum
    while           = "while" ~alnum
    through         = "through" ~alnum
    by              = "by" ~alnum
    mod             = "mod" ~alnum
    poop            = "poop" ~alnum
    walkies         = "walkies" ~alnum
    cat             = "cat" ~alnum
    keyword         = equalityop 
                    | relop 
                    | type
                    | is 
                    | chase 
                    | tail 
                    | else 
                    | bark 
                    | woof
                    | howl
                    | trick 
                    | if 
                    | then 
                    | good 
                    | bad 
                    | chews 
                    | fetches 
                    | give 
                    | with 
                    | without 
                    | not
                    | peanutButter
                    | times
                    | while
                    | through
                    | by
                    | mod
                    | poop
                    | walkies
                    | cat
    id              = ~keyword (letter | "_") (letter | digit | "_")*
    space           += comment
    comment         = "!!!" (~"!!!" any)* "!!!"
}
